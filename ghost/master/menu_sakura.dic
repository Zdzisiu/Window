

//Because there is an all modifier here, you don't need to write -- between menu options
OnSakuraMenu : all
{
	OnSurfaceRestore
	--
	"\0\b[2]\![set,autoscroll,disable]"
	
	"\n\n\_q"
	
	"\![*]\q[Inventory,OnInventory]  "
	
	"\![*]\q[Config,OnConfig]\n\n"
	
	"\![*]\q[Cancel,blank]"	
}

OnInventory : all
{
	"You reach under the window . . .\n\n[half]\_q"
	_display = "\b2"

	for _i = 0; _i < ARRAYSIZE(items); _i++
	{
		if inventory[_i] == 1; _display += "\![*]\__q[OnChangeItem," + items[_i] + "," + _i + "]" + items[_i] +"\__q\n"
	}

	/*foreach items; _item 
	{
		_display += "\__q[OnChangeItem," + _item + "]" + _item +"\n"
	}*/

	_display
}

OnChangeItem : all
{
	if !WearingDressup('0,Item,'+ reference0)
	{
		"You place the %(reference0) on the windowsill . . ."
		"\![bind,item,%(reference0),1]"
		CurrentItem = reference1
		prevBG = currBG
		currBG = BackgroundArray[CurrentItem]
		--
		if reference0 == "Radio"; "\![sound,loop,trombone.wav]"
		else; "\![sound,stop,trombone.wav]"
		--
		if interactions[CurrentItem] != 1; OnInteract(CurrentItem)
		else; OnRestore
	}
	else
	{
		"You put the %(reference0) away . . ."
		"\![bind,item,%(reference0),0]"
		CurrentItem = -1
		prevBG = currBG

		if SetBG != "OFF"; currBG = SetBG
		else; currBG = ""
		--
		OnRestore
	}
}


OnInteract : all
{
	"\b[-1]\c%(close)\s[0]%(GetBG)\i[104]\w8\w8\![anim,clear,104]"
	--
	case _argv[0]
	{
		when 0
		{
			"\i[103]\i[20,wait]"
			"\![anim,clear,103]\i[101]"
			"\![set,balloontimeout,-1]\*Uuuuuh what an amazing buffet\n\n[half]"
			"\![*]\__q[OnResponse,%(_argv[0]),1,1]Truly feeds ones mind doesn't it?\__q\n"
			"\![*]\__q[OnResponse,%(_argv[0]),1,2]What do you mean?\__q"
		}
		when 1
		{
			_rand = 0
			_inventoryC = TOINT(ARRAYADD(inventory))

			while TOINT(ARRAYADD(inventory)) < (_inventoryC + 1)
			{
				_rand = RAND(11)
				if _rand != 6; inventory[_rand] = 1
			}

			--
			"\i[103]\i[22,wait]"
			"\![anim,clear,103]\i[101]"
			"\![anim,clear,101]\i[102,wait]\w3\![anim,clear,102]\i[104]"
			"\![anim,clear,22]%(open)"
			if TOINT(ARRAYADD(inventory)) < ARRAYSIZE(items); "%(items[_rand]) lies on the ground."
			interactions[1] = 1
		}
		when 2
		{
			"\i[103]\i[23,wait]"
			"\![anim,clear,103]\i[101]"
			"\![set,balloontimeout,-1]\*Oooooh what a nice light, hello there friend, say, has anything interesting happen lately?\n\n[half]"
			"\![*]\__q[OnResponse,%(_argv[0]),1,1]Hmm, don't think so, why?\__q\n"
			"\![*]\__q[OnResponse,%(_argv[0]),1,2]Hmmm, there was some guy shouting at the trees for 'greedily stealing the sun' I guess?\__q"
		}
		when 3
		{
			_rand = 0
			_inventoryC = TOINT(ARRAYADD(inventory))

			while TOINT(ARRAYADD(inventory)) < (_inventoryC + 1)
			{
				_rand = RAND(11)
				if _rand != 6; inventory[_rand] = 1
			}
			--
			"\i[103,wait]"
			"\i[37,wait]\![anim,clear,37]\i[2006]\![anim,clear,2006]\i[2007]\w8\![anim,clear,2007]\i[38,wait]"
			"\![anim,clear,103]\i[101]"
			"\![anim,clear,101]\i[102,wait]\w3\![anim,clear,102]\i[104]"
			"\![anim,clear,38]%(open)"
			if TOINT(ARRAYADD(inventory)) < ARRAYSIZE(items); "lies on the ground."
			interactions[3] = 1
		}
		when 4
		{
			"\i[103]\i[25,wait]"
			"\![anim,clear,103]\i[101]"
			"\![set,balloontimeout,-1]\*Hello neighbor, sorry for intruding, it just smelled so familiar. My late wife used to make the best apple pie!\n\n[half]"
			"\![*]\__q[OnResponse,%(_argv[0]),1,1]Sorry for your loss\__q\n"
			"\![*]\__q[OnResponse,%(_argv[0]),1,2]Would you like some?\__q"
		}
		when 5
		{
			"\i[103]\i[27,wait]"
			"\![anim,clear,103]\i[101]"
			"\![set,balloontimeout,-1]\*Hello, hello! A fellow bird fan I see, there are so many amazing specimens here, did you know there around 11 thousand bird species around the world!? How is someone to witness them all, a true tragedy, and yet, with so many birds around, one might always spot a new on, oh the blessing!\n\n[half]"
			"\![*]\__q[OnResponse,%(_argv[0]),1,1]What are you talking about??\__q\n"
			"\![*]\__q[OnResponse,%(_argv[0]),1,2]Wow wow, Slow down\__q"
		}
		when 6
		{
			"\i[103]\i[29,wait]"
			"\![anim,clear,103]\i[101]"
			"\![set,balloontimeout,-1]\*\w4. \w4. \w4. \w4. \w4. \w4. \w4. \w4. \w4.\n\n[half]"
			"\![*]\__q[OnResponse,%(_argv[0]),1,1]Hello?\__q\n"
			"\![*]\__q[OnResponse,%(_argv[0]),1,2]Are you alright?\__q"
		}
		when 7
		{
			"\i[103]\i[31,wait]"
			"\![anim,clear,103]\i[101]"
			"\![set,balloontimeout,-1]\*The past is not dead\n\n[half]"
			"\![*]\__q[OnResponse,%(_argv[0]),1,1]What?\__q\n"
			"\![*]\__q[OnResponse,%(_argv[0]),1,2]Why?\__q"
		}
		when 8
		{
			"\i[103]\i[33,wait]"
			"\![anim,clear,103]\i[101]"
			"\![set,balloontimeout,-1]\*May I have some water?\n\n[half]"
			"\![*]\__q[OnResponse,%(_argv[0]),1,1]\f[italic,1]* Give him water\f[italic,0]\__q\n"
			"\![*]\__q[OnResponse,%(_argv[0]),1,2]I have none to spare, sorry\__q"
		}
		when 9
		{
			_rand = 0
			_inventoryC = TOINT(ARRAYADD(inventory))

			while TOINT(ARRAYADD(inventory)) < (_inventoryC + 1)
			{
				_rand = RAND(11)
				if _rand != 6; inventory[_rand] = 1
			}
			--
			"\i[103,wait]"
			"\i[2003,wait]\![anim,clear,2003]\i[2004]\w8\w8\w8\![anim,clear,2004]\i[2005]"
			"\![anim,clear,103]\i[101]"
			"\![anim,clear,101]\i[102,wait]\w3\![anim,clear,102]\i[104]"
			"\![anim,clear,2005]%(open)"
			if TOINT(ARRAYADD(inventory)) < ARRAYSIZE(items); "%(items[_rand]) lies on the ground."
			interactions[9] = 1
		}
		when 10
		{
			"\i[35,wait]%(open)"
			"\![set,balloontimeout,-1]\*You should watch out for the Bies\n\n[half]"
			"\![*]\__q[OnResponse,%(_argv[0]),1,1]A what?\__q\n"
			"\![*]\__q[OnResponse,%(_argv[0]),1,2]Are you not dangerous as well?\__q"
		}
	}
}

OnResponse
{
	if TOINT(ARRAYADD(inventory)) < ARRAYSIZE(items)
	{
		_rand = 0
		_inventoryC = TOINT(ARRAYADD(inventory))

		if TOINT(reference0) != 7
		{
			while TOINT(ARRAYADD(inventory)) < (_inventoryC + 1)
			{
				_rand = RAND(11)
				if _rand != 6; inventory[_rand] = 1
			}
		}
		else
		{
			inventory[6] = 1
		}
	}
	--
	case TOINT(reference0)
	{
		when 0
		{
			case reference1
			{
				when 1
				{
					if reference2 == 1; "Feeds the mind, sure, of course of course, that's what I meant nothing else."
					elseif reference2 == 2; "Books are just my favorite."
					--
					if TOINT(ARRAYADD(inventory)) < ARRAYSIZE(items); "\xThe figure akwardly hurries off, OH, they dropped %(items[_rand])!"
					else; "\xThe figure akwardly hurries off."
					--
					"\![anim,clear,20]\i[21,wait]\![anim,clear,103]\i[101]\![anim,clear,101]\i[102,wait]\w3\![anim,clear,102]\i[104]\![anim,clear,21]%(open)"
				}
			}
		}
		when 2
		{
			case reference1
			{
				when 1
				{
					if reference2 == 1; "What a shame it is to have no good story to share."
					elseif reference2 == 2; "Uuuuh yes yes, the trees really do steal all of the sun, never leaving anything for us, forcing us into the shadows."
					--
					if TOINT(ARRAYADD(inventory)) < ARRAYSIZE(items); "\xThey notice another light and leave, losing their %(items[_rand])."
					else; "\xThey notice another light and leave."
					--
					"\![anim,clear,23]\i[24,wait]\![anim,clear,103]\i[101]\![anim,clear,101]\i[102,wait]\w3\![anim,clear,102]\i[104]\![anim,clear,24]%(open)"
				}
			}
		}
		when 4
		{
			case reference1
			{
				when 1
				{
					if reference2 == 1; "No need to worry, it was time, she lived a long life full of the things she loved, we all have to leave someday and she made peace with it."
					elseif reference2 == 2; "No need, I had my share of pie in life, at this point I prefer things with a lot less sugar, but I do still enjoy the smell of it."
					--
					if TOINT(ARRAYADD(inventory)) < ARRAYSIZE(items); "\xAfter a bit of a short chat, the old man gives you %(items[_rand]) and leaves to continue his walk."
					else; "\xAfter a bit of a short chat, the old man leaves to continue his walk."
					--
					"\![anim,clear,25]\i[26,wait]\![anim,clear,103]\i[101]\![anim,clear,101]\i[102,wait]\w3\![anim,clear,102]\i[104]\![anim,clear,26]%(open)"
				}
			}
		}
		when 5
		{
			case reference1
			{
				when 1
				{
					if reference2 == 1; "Birds, what else!? They have such amazing feathers, and so many different looking eggs! Their nests a marvel of nature and engineering, the plumage so vibrant, oh the blessing to live in a world full of those amazing creatures!"
					elseif reference2 == 2; "But there is no time! We need to hurry to see as many as possible, to witness them and their amazing habits, so many of them are endangered, what if I'm too late?! There are so many birds that I will never get to see, I can't add more to this list!"
					--
					if TOINT(ARRAYADD(inventory)) < ARRAYSIZE(items); "\xThey hurry off, probably to find more birds, in their hurry they dropped %(items[_rand])."
					else; "\xThey hurry off, probably to find more birds."
					--
					"\![anim,clear,27]\i[28,wait]\![anim,clear,103]\i[101]\![anim,clear,101]\i[102,wait]\w3\![anim,clear,102]\i[104]\![anim,clear,28]%(open)"
				}
			}
		}
		when 6
		{
			case reference1
			{
				when 1
				{
					"\w6. \w6. \w6. \w6. \w6. \w6. \w6. \w6. \w6."
					--
					if TOINT(ARRAYADD(inventory)) < ARRAYSIZE(items); "\xIt left, leaving behind %(items[_rand]), weird."
					else; "\xIt left, weird."
					--
					"\![anim,clear,29]\i[30,wait]\![anim,clear,103]\i[101]\![anim,clear,101]\i[102,wait]\w3\![anim,clear,102]\i[104]\![anim,clear,30]%(open)"
				}
			}
		}
		when 7
		{
			case reference1
			{
				when 1
				{
					"It's not even past."
					--
					if TOINT(ARRAYADD(inventory)) < ARRAYSIZE(items); "\xHe hands you a knife."
					else; "\xHe leaves"
					--
					"\![anim,clear,31]\i[32,wait]\![anim,clear,103]\i[101]\![anim,clear,101]\i[102,wait]\w3\![anim,clear,102]\i[104]\![anim,clear,32]%(open)"
				}
			}
		}
		when 8
		{
			case reference1
			{
				when 1
				{
					if reference2 == 1; "You have my gratitude, stranger."
					elseif reference2 == 2; "That's understandable."
					--
					if TOINT(ARRAYADD(inventory)) < ARRAYSIZE(items); "\xThe figure leaves you %(items[_rand]), and carries on."
					else; "\xThe figure carries on."
					--
					"\![anim,clear,33]\i[34,wait]\![anim,clear,103]\i[101]\![anim,clear,101]\i[102,wait]\w3\![anim,clear,102]\i[104]\![anim,clear,34]%(open)"
				}
			}
		}
		when 10
		{
			case reference1
			{
				when 1
				{
					if reference2 == 1; "Bies, a sickness, a plague, the one destroying the farmland. Do not seek it's treasures, it never ends well."
					elseif reference2 == 2; "Respect my forest and the creatures within it, and I will have no reason to cause you harm."
					--
					if TOINT(ARRAYADD(inventory)) < ARRAYSIZE(items); "\xThe old man left, huh, he left something on your window, %(items[_rand])? Why would he carry that around?%(close)"
					else; "\xThe old man left."
					--
					"\![anim,clear,34]%(close)\![anim,clear,35]%(open)"
				}
			}
		}
	}
	--
	interactions[TOINT(reference0)] = 1
}

OnConfig : all
{	
	OnSurfaceRestore
	"\b2\_q"
	"Current Time: \f[bold,1]" + time + "\__q\f[bold,0]\n"
	"\![*]Time Sync: \f[bold,1]\__q[OnButtonChange,Sync]" + onnoff(syncStat) + "\__q\f[bold,0]\n"
	"\![*]Set Time: \f[bold,1]\__q[OnButtonChange,SetTime]" + SetTime + "\__q\f[bold,0]\n"
	"\![*]Set Backgorund: \f[bold,1]\__q[OnButtonChange,SetBG]" + SetBG + "\__q\f[bold,0]\n"
	"\![*]Remove Item Backgorund: \f[bold,1]\__q[OnButtonChange,RemoveBG]" + onnoff(BackgroundOverride) + "\__q\f[bold,0]\n"
	"\n\![*]\q[Cancel,blank]"
}

OnButtonChange
{
	_display = "Change to . . . \_q\n\n[half]"
	--
	if reference0 == "Sync"
	{
		if syncStat == 0
		{
			SetTime = "OFF"
			syncStat = 1
		}
		else
		{
			prevTime = currTime
			currTime = TOINT(time)
			SetTime = "Day"
			syncStat = 0
		}
		--
		OnConfig
	}
	elseif reference0 == "SetTime"
	{
		foreach timeArray; _time_name
		{
			_display += "\__q[OnButtonChange,time," + _time_name + "]" + _time_name + "\__q\n"
		}
		_display += "\__q[OnButtonChange,time,OFF]OFF\__q\n"
		_display += "\n\![*]\q[Cancel,blank]"
		_display
	}
	elseif reference0 == "SetBG"
	{
		for _i = 0; _i < ARRAYSIZE(items); _i++
		{
			if inventory[_i] == 1; _display += "\![*]\__q[OnButtonChange,BG," + BackgroundArray[_i] + "]" + BackgroundArray[_i] +"\__q\n"
		}
		_display += "\__q[OnButtonChange,BG,OFF]OFF\__q\n"
		_display += "\n\f[italics,1]Note: Setting a background overrides the item backgrounds\f[italics,0]\n"
		_display += "\n\![*]\q[Cancel,blank]"
		_display
	}
	elseif reference0 == "time"
	{
		prevTime = currTime

		if reference1 == "OFF"
		{
			if syncStat == 1; currTime = time
			else; currTime = "Day"
		}
		else; currTime = reference1

		SetTime = reference1
		syncStat = 0
		--
		OnConfig
	}
	elseif reference0 == "BG"
	{
		prevBG = currBG

		if reference1 == "OFF"; currBG = ""
		else; currBG = reference1

		SetBG = reference1
		syncStat = 0
		--
		OnConfig
	}
	elseif reference0 == "RemoveBG"
	{
		if BackgroundOverride == 0
		{
			prevBG = currBG
			currBG = ""
			BackgroundOverride = 1
		}
		else
		{
			prevBG = currBG
			currBG = CurrentItem
			BackgroundOverride = 0
		}
		--
		OnConfig
	}
}